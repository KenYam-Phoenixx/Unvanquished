selector
{
	condition ( aliveTime > 1500 && healScore < 0.5 )
	{
		action evolve
	}

	sequence
	{
		condition alertedToEnemy
		selector
		{
			decorator timer( 1000 )
			{
				selector
				{
					sequence
					{
						condition ( distanceTo( E_A_OVERMIND ) <= 400 || distanceTo( E_A_BOOSTER ) <= 400 )
						action fight
					}
					sequence
					{
						condition ( healScore > 0.5 && percentHealth( E_GOAL ) > 0.3 )
						condition baseRushScore < 0.75
						action heal
					}
				}
			}

			action fight
		}
	}

	// aliens should only try to do opportunistic resupply for poison, or the will end up camping too much.
	// also, we want to be sure the distance to the booster is not too big to prevent infinite back'n forth
	condition resupplyScore > 0.90 && distanceTo( E_A_BOOSTER ) <= 1000
	{
		action moveTo( E_A_BOOSTER )
	}

	selector
	{
		sequence
		{
			condition healScore > 0.25
			action heal
		}
	}

	condition baseRushScore > 0.5
	{
		action rush
	}

	action roam
}
